apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = modVersion
group = modGroup
archivesBaseName = 'EclipsisCommon'

minecraft {
    version = project.forgeVersion
    mappings = project.mcpVersion
    runDir = 'run'
}

repositories {
    maven {
        url 'http://maven.shadowfacts.net/'
    }
    maven {
        url 'http://maven.bluexin.be/repository/snapshots/'
    }
    mavenCentral()
}

dependencies {
    implementation('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: '*'
    }
    compile 'net.shadowfacts:Forgelin:1.8.2'
    compile 'com.teamwizardry.librarianlib:librarianlib-1.12.2:4.17-SNAPSHOT'

    //Needed for MixinAvailabilityServer character level redirect
    compileOnly 'org.spongepowered:spongeapi:7.1.0'
    compileOnly 'com.github.Sponge-RPG-dev.NT-RPG:NT-RPG:class-refactor-SNAPSHOT'
}

sourceSets {
    main {
        ext.refMap = 'mixins.glassspirit.refmap.json'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes(
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs': 'mixins.glassspirit.json',
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true',
                'TweakOrder': '0',
                'FMLAT': 'cnpcs_at.cfg' //Needed for CustomNPCs shadowing
        )
    }
}

shadowJar {
    classifier = null
    exclude 'dummyThing'
    exclude '/META-INF/*.json'
    dependencies {
        exclude(dependency('::')) // only shade CustomNPCs
    }
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

build.dependsOn(shadowJar)
