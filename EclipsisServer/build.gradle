apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.spongepowered.plugin'

version = modVersion
group = modGroup
archivesBaseName = 'CustomRPG_Server'

sponge {
    plugin {
        meta {
            id = 'eclipsis_plugin'
        }
    }
}

minecraft {
    version = project.forgeVersion
    mappings = project.mcpVersion
    runDir = 'run'
}

dependencies {
    implementation('org.spongepowered:spongeforge:1.12.2-2768-7.1.6-RC3639:dev') {
        exclude module: 'testmods'
    }
    implementation('com.github.Sponge-RPG-dev.NT-RPG:NT-RPG:2.0.0-SNAPSHOT-9')
    annotationProcessor('com.github.Sponge-RPG-dev.NT-RPG:Generator:2.0.0-SNAPSHOT-9')
    implementation(project(':EclipsisCommon'))
}

sourceSets {
    main {
        ext.refMap = 'mixins.glassspirit.server.refmap.json'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes(
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs': 'mixins.glassspirit.server.json',
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true',
                'TweakOrder': '0'
        )
    }
}